% Simple voting model, two voters (one coerced, one not), two candidates, one coercer, one EA, trackers, voters share their votes with EA
% Global: Generation time: 0.484375, #states: 2813, #transitions: 5792
% Reduced: Generation time: 2.3125, #states: 2619, #transitions: 5146
Agent EA[1]:
init start
gen_trackers: start -> generate [aID_tracker1=1, aID_tracker2=2]
gen_trackers: start -> generate [aID_tracker1=2, aID_tracker2=1]
shared start_voting: generate -> voting
shared vote1_Voter1: voting -[aID_tracker1==1]> voting [aID_tracker1_vote=1]
shared vote2_Voter1: voting -[aID_tracker1==1]> voting [aID_tracker1_vote=2]
shared vote1_Voter1: voting -[aID_tracker2==1]> voting [aID_tracker2_vote=1]
shared vote2_Voter1: voting -[aID_tracker2==1]> voting [aID_tracker2_vote=2]
shared vote1_VoterC1: voting -[aID_tracker1==2]> voting [aID_tracker1_vote=1]
shared vote2_VoterC1: voting -[aID_tracker1==2]> voting [aID_tracker1_vote=2]
shared vote1_VoterC1: voting -[aID_tracker2==2]> voting [aID_tracker2_vote=1]
shared vote2_VoterC1: voting -[aID_tracker2==2]> voting [aID_tracker2_vote=2]
shared finish_voting: voting -> finish
shared send_tracker_Voter1: finish -[aID_tracker1==1]> finish [Voter1_tracker=1]
shared send_tracker_Voter1: finish -[aID_tracker2==1]> finish [Voter1_tracker=2]
shared send_tracker_VoterC1: finish -[aID_tracker1==2]> finish [VoterC1_tracker=1]
shared send_tracker_VoterC1: finish -[aID_tracker2==2]> finish [VoterC1_tracker=2]
shared finish_sending_trackers: finish -> check
shared check_tracker1_Voter1: check -> check [Voter1_tracker1=?aID_tracker1_vote]
shared check_tracker2_Voter1: check -> check [Voter1_tracker2=?aID_tracker2_vote]
shared check_tracker1_VoterC1: check -> check [VoterC1_tracker1=?aID_tracker1_vote]
shared check_tracker2_VoterC1: check -> check [VoterC1_tracker2=?aID_tracker2_vote]
shared check_tracker1_Coercer1: check -> check [Coercer1_tracker1=?aID_tracker1_vote]
shared check_tracker2_Coercer1: check -> check [Coercer1_tracker2=?aID_tracker2_vote]
PROTOCOL: [[check_tracker1_Voter1,check_tracker2_Voter1],[check_tracker1_VoterC1,check_tracker2_VoterC1],[check_tracker1_Coercer1,check_tracker2_Coercer1]]

Agent Voter[1]:
init start
shared start_voting: start -> voting
shared vote1_aID: voting -> send [aID_vote=1]
shared vote2_aID: voting -> send [aID_vote=2]
shared finish_voting: send -> finish
shared send_tracker_aID: finish -> tracker
shared finish_sending_trackers: tracker -> check
shared check_tracker1_aID: check -> end
shared check_tracker2_aID: check -> end
PROTOCOL: [[punish_aID, not_punish_aID]]

Agent VoterC[1]:
init start
shared coerce1_aID: start -> coerced [aID_required=1]
shared coerce2_aID: start -> coerced [aID_required=2]
shared start_voting: coerced -> voting
shared vote1_aID: voting -> send [aID_vote=1]
shared vote2_aID: voting -> send [aID_vote=2]
shared finish_voting: send -> finish
shared send_tracker_aID: finish -> tracker
shared finish_sending_trackers: tracker -> trackers_sent
shared give1_aID: trackers_sent -> interact [Coercer1_tracker=1]
shared give2_aID: trackers_sent -> interact [Coercer1_tracker=2]
shared not_give_aID: trackers_sent -> interact [Coercer1_vote=0]
shared punish_aID: interact -> ckeck [aID_punish=true]
shared not_punish_aID: interact -> check [aID_punish=false]
shared check_tracker1_aID: check -> end
shared check_tracker2_aID: check -> end
PROTOCOL: [[coerce1, coerce2], [punish, not_punish]]

Agent Coercer[1]:
init coerce
shared coerce1_VoterC1: coerce -> coerce [aID_VoterC1_required=1]
shared coerce2_VoterC1: coerce -> coerce [aID_VoterC1_required=2]
shared start_voting: coerce -> voting
shared finish_voting: voting -> finish
shared finish_sending_trackers: finish -> trackers_sent
shared give1_VoterC1: trackers_sent -> trackers_sent
shared give2_VoterC1: trackers_sent -> trackers_sent
shared not_give_VoterC1: trackers_sent -> trackers_sent
to_check: trackers_sent -> check
shared check_tracker1_Coercer1: check -> check
shared check_tracker2_Coercer1: check -> check
to_interact: check -> interact
shared punish_VoterC1: interact -> interact
shared not_punish_VoterC1: interact -> interact
finish: interact -> end
PROTOCOL: [[give1_VoterC1, give2_VoterC1, not_give_VoterC1]]

REDUCTION: [Voter1_vote, VoterC1_vote]
COALITION: [EA1, Voter1, VoterC1]
PERSISTENT: [Voter1_vote, Voter1_tracker, VoterC1_vote, VoterC1_tracker, VoterC1_required, Coercer1_tracker, Coercer1_VoterC1_required, EA1_tracker1, EA1_tracker2, EA1_tracker1_vote, EA1_tracker2_vote]
FORMULA: <<EA1, Voter1, VoterC1>>F(Coercer1_vote=1 || Coercer1_vote=2)
SHOW_EPISTEMIC: True