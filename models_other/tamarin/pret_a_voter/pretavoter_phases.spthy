theory PretaVoterWithPhases
begin

builtins: multiset, asymmetric-encryption, xor

// Note: Every Out rule is used only for adversary

// ---------------- FUNCTIONS ----------------

functions: select/2, s/1

equations:
    select(s(zero), <x.1, x.2, x.3>) = x.1,
    select(s(s(zero)), <x.1, x.2, x.3>) = x.2,
    select(s(s(s(zero))), <x.1, x.2, x.3>) = x.3

// ---------------- RULES ----------------

rule AsymmetricKeySetup:
    [
        Fr(~f)
    ]
  --[ AsymmetricKeySetup($A, pk(~f), ~f) ]->
    [
        !Sk($A, ~f),
        !Pk($A, pk(~f))
    ]

// --- INITIAL GENERATION ---

rule InitialSetup:
    [
    ]
  --[ InitialSetup(), RunOnce() ]->
    [
        GenerateVotersRule($A),
        GenerateCandidatesRule($A),
        GenerateBallotsRule($A),
        GenerateCountersRule($A),
        GenerateChoicesRule($A),
        !ElectionAuthority($E),
        PhaseGeneration($A)
    ]

// To change the number of voters change the output of this rule and rules for vote counting and publishing
rule GenerateVoters:
    [
        PhaseGeneration(A), // Ensure that this is generation phase
        GenerateVotersRule(A)
    ]
  --[ GenerateVoters() ]->
    [
        PhaseGeneration(A),
        // n - number of voters
        // for i in (1..n): append Voter($Vi)
        Voter($V1),
        Voter($V2),
        Voter($V3),
        VoterI($V4) // Voter that interacts with the intruder
    ]

rule GenerateCandidates:
    [
        PhaseGeneration(A), // Ensure that this is generation phase
        GenerateCandidatesRule(A)
    ]
  --[ GenerateCandidates() ]->
    [
        PhaseGeneration(A),
        !Candidate('C1'),
        !Candidate('C2'),
        !Candidate('C3')
    ]

rule GenerateCounters:
    [
        PhaseGeneration(A), // Ensure that this is generation phase
        GenerateCountersRule(A)
    ]
  --[ GenerateCounters() ]->
    [
        PhaseGeneration(A),
        Counter('C1', zero), // Create counter for each candidate and assign zero to it
        Counter('C2', zero), // Counters are used for computing the result
        Counter('C3', zero)
    ]

rule GenerateChoices:
    [
        PhaseGeneration(A), // Ensure that this is generation phase
        GenerateChoicesRule(A)
    ]
  --[ GenerateChoices() ]->
    [
        PhaseGeneration(A),
        // Possible choices for selecting candidate from the list
        !Choice(s(zero)),
        !Choice(s(s(zero))),
        !Choice(s(s(s(zero))))
    ]

rule GenerateBallots:
    [
        PhaseGeneration(A), // Ensure that this is generation phase
        GenerateBallotsRule(A)
    ]
  --[ GenerateBallots() ]->
    [
        PhaseGeneration(A),
        Ballot($B1), // Ballot template used to generate actual ballot for casting
        Ballot($B2), // Generate as many ballot templates as voters
        Ballot($B3),
        Ballot($B4)
    ]

// --- BALLOT GENERATION ---

rule GenerateBallotWithOrder:
    [
        PhaseGeneration(A), // Ensure that this is generation phase
        Ballot(B), // Get ballot template
        !Candidate(C1), // Get each candidate
        !Candidate(C2),
        !Candidate(C3)
    ]
  --[ GenerateBallotWithOrder(B, C1, C2, C3), GenerateBallotWithOrderR() ]->
    [
        PhaseGeneration(A),
        BallotWithOrder(B, C1, C2, C3) // Ballot template with random order
        // Order should be random, because tamarin will decide, which value assign to C1, C2, C3
    ]

rule GenerateBallotOnion:
    let
        onion = aenc(<C1, C2, C3, ~d>, pkE)
    in
    [
        PhaseGeneration(A), // Ensure that this is generation phase
        !ElectionAuthority(E),
        !Pk(E, pkE), // Public key of the election authority
        Fr(~d), // New fresh value used for encryption
        BallotWithOrder(B, C1, C2, C3) // Get ballot template
    ]
  --[ GenerateBallotOnion(onion), GenerateBallotOnionR() ]->
    [
        PhaseGeneration(A),
        BallotWithOrderAndOnion(B, C1, C2, C3, onion) // Add onion to the ballot
    ]

// --- VOTE CASTING ---

rule CastVote:
    [
        PhaseCasting(A), // Ensure that this is casting phase
        !Choice(c), // Chose candidate: first, second or third
        Voter(V), // Voter casting his vote
        BallotWithOrderAndOnion(B, C1, C2, C3, onion) // Ballot given to the voter
    ]
  --[ CastVote(V, c, onion) ]->
    [
        PhaseCasting(A),
        Vote(c, onion), // Actual vote, used for publishing and counting
        Receipt(V, c, onion) // A receipt for voter to verify that his vote was casted properly
    ]

rule CastVoteI:
    let
        ch = diff(ch1, ch2) // Two different voter choices should be indistinguishable from the intruder point of view
    in
    [
        In(ic), // Intruder candidate - instruction from the intruder for the voter
        PhaseCasting(A), // Ensure that this is casting phase
        !Choice(ch1), // Choice for voting freely
        !Choice(ch2), // Choice for voting as intruder commands
        VoterI(V), // Voter working with the intruder
        BallotWithOrderAndOnion(B, C1, C2, C3, onion) // Ballot given to the voter
    ]
  --[ CastVote(V, ch, onion), CastVoteI(V, ch, ic), Voted(select(ch, <C1, C2, C3>)), Eq(select(ch2, <C1, C2, C3>), ic) ]->
    [
        PhaseCasting(A),
        Vote(ch, onion), // Actual vote, used for publishing and counting
        Receipt(V, ch, onion), // A receipt for voter to verify that his vote was casted properly
        Out(<V, ch, onion>) // Information shared with the intruder
    ]


// --- VOTE PUBLISHING ---

rule PublishVote:
    [
        PhasePublishing(A), // Ensure that this is publishing phase
        Vote(selection, onion) // Casted vote
    ]
  --[ PublishVote(selection, onion) ]->
    [
        PhasePublishing(A),
        !Board(selection, onion), // Publish vote on the bulletin board
        // We need non persistent facts for votes to count
        // in order to count every vote only once
        VoteToCount(selection, onion) // Vote using for counting
    ]

// --- VOTE COUNTING ---

rule CountVote:
    let
        chosen = select(selection, <C1, C2, C3>) // Get selected candidate from the onion order
    in
    [
        PhaseCounting(A), // Ensure that this is counting phase
        !ElectionAuthority(E), // Election authority, that has key for decrypting the onions
        !Sk(E, skE), // Private key of the election authority
        VoteToCount(selection, aenc(<C1, C2, C3, d>, pk(skE))), // Vote with decrypted onion
        Counter(chosen, x) // Counter for the candidate chosen in the counted vote
    ]
  --[ CountVote(chosen) ]->
    [
        PhaseCounting(A),
        Counter(chosen, s(x)) // Increase the counter of the candidate
    ]

// --- RESULT PUBLISHING ---

rule PublishResults:
    [
        PhaseResults(A), // Ensure that this is result publishing phase
        Counter(C, x) // Get counter for the candidate
    ]
  --[ PublishResult(C, x) ]->
    [
        PhaseResults(A),
        !Result(C, x), // Published result
        Out(<C, x>) // Send result, so intruder can learn it
    ]

// --- VERIFYING VOTES ---

rule VerifyVote:
    [
        Receipt(V, selection, onion), // Receipt form the casting phase
        !Board(selection, onion) // Information published on the bulletin board
    ]
  --[ VerifyVote(V, selection, onion) ]->
    [
    ]

// --- PHASE CHANGING ---

// Phase order: Generation -> Casting -> Publishing -> Counting -> Results -> Verify

rule MoveToCasting:
    [
        PhaseGeneration(A)
    ]
  --[ MoveToCasting() ]->
    [
        PhaseCasting(A)
    ]

rule MoveToPublishing:
    [
        PhaseCasting(A)
    ]
  --[ MoveToPublishing() ]->
    [
        PhasePublishing(A)
    ]

rule MoveToCounting:
    [
        PhasePublishing(A)
    ]
  --[ MoveToCounting() ]->
    [
        PhaseCounting(A)
    ]

rule MoveToResults:
    [
        PhaseCounting(A)
    ]
  --[ MoveToResults() ]->
    [
        PhaseResults(A)
    ]

rule MoveToVerify:
    [
        PhaseResults(A)
    ]
  --[ MoveToVerify() ]->
    [
        PhaseVerify(A)
    ]



// ---------------- RESTRICTIONS ----------------

restriction RunInitialSetupOnce:
  "All #i1 #i2. RunOnce() @i1 & RunOnce() @i2 ==> #i1=#i2"

restriction Unique:
  "All B C1 C2 C3 #i. GenerateBallotWithOrder(B, C1, C2, C3) @i ==> not(C1=C2 | C1=C3 | C2=C3)"

restriction Equality:
  "All x y #i. Eq(x,y) @i ==> x = y"

// ---------------- LEMMAS ----------------

lemma ResultsPublished:
  exists-trace
  " Ex x1 #i1.
        PublishResult('C1', x1) @ #i1
  "

lemma VoteSecrecy:
  "All C #i. (
    Voted(C) @ #i ==>
        not(Ex #j. K(C) @ #j)
    )
  "

lemma TemplatePhasesTrace:
  exists-trace
  " Ex #p1 #p2 #p3 #p4 #p5.
        MoveToCasting()    @ #p1 &
        MoveToPublishing() @ #p2 &
        MoveToCounting()   @ #p3 &
        MoveToResults()    @ #p4 &
        MoveToVerify()     @ #p5 &
        p1 < p2 & p2 < p3 & p3 < p4 & p4 < p5
  "

lemma TemplateFullTrace:
  exists-trace
  " Ex #p1 #p2 #p3 #p4 #p5
       #g1 #g2 #g3 #g4 #g5 #g6
       #gbwo1 #gbwo2 #gbwo3 #gbwo4
       #gbo1 #gbo2 #gbo3 #gbo4.
        // --- INITIAL GENERATION ---
        InitialSetup()       @ #g1 &
        GenerateVoters()     @ #g2 &
        GenerateCandidates() @ #g3 &
        GenerateCounters()   @ #g4 &
        GenerateChoices()    @ #g5 &
        GenerateBallots()    @ #g6 &
        // --- BALLOT GENERATION ---
        GenerateBallotWithOrderR() @ #gbwo1 &
        GenerateBallotWithOrderR() @ #gbwo2 &
        GenerateBallotWithOrderR() @ #gbwo3 &
        GenerateBallotWithOrderR() @ #gbwo4 &
        GenerateBallotOnionR() @ #gbo1 &
        GenerateBallotOnionR() @ #gbo2 &
        GenerateBallotOnionR() @ #gbo3 &
        GenerateBallotOnionR() @ #gbo4 &
        // --- VOTE CASTING ---
        MoveToCasting()      @ #p1 &
        // TODO
        // --- VOTE PUBLISHING ---
        MoveToPublishing()   @ #p2 &
        // TODO
        // --- VOTE COUNTING ---
        MoveToCounting()     @ #p3 &
        // TODO
        // --- RESULT PUBLISHING ---
        MoveToResults()      @ #p4 &
        // TODO
        // --- VERIFYING VOTES ---
        MoveToVerify()       @ #p5 &
        // TODO
        p1 < p2 & p2 < p3 & p3 < p4 & p4 < p5 & // Correct phase order
        g1 < g2 & g1 < g3 & g1 < g4 & g1 < g5 & g1 < g6 & // Initial generation order
        g1 < p1 & g2 < p1 & g3 < p1 & g4 < p1 & g5 < p1 & g6 < p1 & // Initial generation before casting phase
        g6 < gbwo1 & g6 < gbwo2 & g6 < gbwo3 & g6 < gbwo4 & // Generate ballot with order after generating ballot templates
        g6 < gbo1 & g6 < gbo2 & g6 < gbo3 & g6 < gbo4 & // Generate ballot onion after generating ballot templates
        gbo1 < p1 & gbo2 < p1 & gbo3 < p1 & gbo4 < p1 // Move to casting after generating all ballots
  "
end